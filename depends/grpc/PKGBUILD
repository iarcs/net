pkgname=grpc
pkgver=1.43.2
_gtestver=c9ccac7cb7345901884aabf5d1a786cfa6e2f397
pkgrel=1
pkgdesc="High performance, open source, general RPC framework that puts mobile and HTTP/2 first"
arch=('x86_64')
url='https://grpc.io'
license=('BSD')
depends=('c-ares' 'protobuf' 'openssl' 're2' 'abseil-cpp' 'python' 'python-six'
         'zlib')
makedepends=('re2' 're2c' 'protobuf' 'c-ares' 'openssl' 'chrpath' 'cython'
             'cmake' 'benchmark' 'python-setuptools' 'abseil-cpp' 'ninja')
provides=('grpc' 'python-grpcio' 'grpc-cli')
conflicts=('grpc' 'python-grpcio' 'grpc-cli')
options=('staticlibs')
source=("https://github.com/grpc/grpc/archive/v$pkgver/$pkgname-$pkgver.tar.gz"
        "https://github.com/google/googletest/archive/$_gtestver/googletest-$_gtestver.tar.gz"
        "python-grpcio-use-system-abseil.patch")
sha256sums=('b74ce7d26fe187970d1d8e2c06a5d3391122f7bc1fdce569aff5e435fb8fe780'
            '443d383db648ebb8e391382c0ab63263b7091d03197f304390baac10f178a468'
            'e3f2f4df3eacf04711b82448dd5115e473fbf59490f6d5bf95e24b635a20b127')

prepare() {
    cd "$srcdir/$pkgname-$pkgver"
    patch -Np1 -i "$srcdir/python-grpcio-use-system-abseil.patch"
    ln -sf "$srcdir/googletest-$_gtestver/"{googlemock,googletest} \
        third_party/googletest
}

build() {
    cd "$srcdir/$pkgname-$pkgver"
    cmake -Bbuild \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_CXX_FLAGS="${CXXFLAGS} -DNDEBUG" \
        -DgRPC_INSTALL=ON \
        -DCMAKE_CXX_STANDARD=17 \
        -DCMAKE_SKIP_INSTALL_RPATH=ON \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DgRPC_BUILD_TESTS=ON \
        -DgRPC_BUILD_CODEGEN=ON \
        -DgRPC_BUILD_CSHARP_EXT=ON \
        -DgRPC_BACKWARDS_COMPATIBILITY_MODE=OFF \
        -DgRPC_ZLIB_PROVIDER='package' \
        -DgRPC_CARES_PROVIDER='package' \
        -DgRPC_RE2_PROVIDER='package' \
        -DgRPC_SSL_PROVIDER='package' \
        -DgRPC_PROTOBUF_PROVIDER='package' \
        -DgRPC_PROTOBUF_PACKAGE_TYPE='MODULE' \
        -DgRPC_BENCHMARK_PROVIDER:STRING='package' \
        -DgRPC_ABSL_PROVIDER:STRING='package' \
        -DgRPC_USE_PROTO_LITE=OFF \
        -DgRPC_BUILD_GRPC_CPP_PLUGIN=ON \
        -DgRPC_BUILD_GRPC_CSHARP_PLUGIN=ON \
        -DgRPC_BUILD_GRPC_NODE_PLUGIN=ON \
        -DgRPC_BUILD_GRPC_OBJECTIVE_C_PLUGIN=ON \
        -DgRPC_BUILD_GRPC_PYTHON_PLUGIN=ON \
        -DgRPC_BUILD_GRPC_RUBY_PLUGIN=ON \
        -GNinja

    cd "$srcdir/$pkgname-$pkgver/build"
    cmake --build .
    export LDFLAGS="$LDFLAGS -L$PWD"

    # Python
    cd "$srcdir/$pkgname-$pkgver"
    GRPC_ENABLE_FORK_SUPPORT=1 \
    GRPC_PYTHON_CFLAGS="-fvisibility=hidden -fno-wrapv -fno-exceptions $(pkg-config --cflags protobuf) -std=c++17" \
    GRPC_PYTHON_LDFLAGS="$(pkg-config --libs protobuf)" \
    GRPC_PYTHON_BUILD_WITH_CYTHON=1 \
    GRPC_PYTHON_BUILD_SYSTEM_OPENSSL=1 \
    GRPC_PYTHON_BUILD_SYSTEM_ZLIB=1 \
    GRPC_PYTHON_BUILD_SYSTEM_CARES=1 \
    GRPC_PYTHON_BUILD_SYSTEM_RE2=1 \
    GRPC_PYTHON_BUILD_SYSTEM_ABSL=1 \
    GRPC_BUILD_WITH_BORING_SSL_ASM=0 \
    python setup.py build
}

check() {
    cd "$srcdir/$pkgname-$pkgver"
    _pyver=$(python -c "import sys; print('{0}{1}'.format(*sys.version_info[:2]))")
    PYTHONPATH="python_build/lib.linux-x86_64-cpython-$_pyver" python -c 'import grpc'
}

package() {
    cd "$srcdir/$pkgname-$pkgver/build"
    DESTDIR="$pkgdir" cmake --install .
    install -Dm644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

    # Python
    cd "$srcdir/$pkgname-$pkgver"
    python setup.py install -O1 --skip-build --root="$pkgdir" --prefix=/usr

    # CLI
    cd "$srcdir/$pkgname-$pkgver"
    install -dm0755 "$pkgdir/usr/lib"
    cp -a build/libgrpc++_test_config.* "$pkgdir/usr/lib/"
    install -Dm0755 build/grpc_cli "$pkgdir/usr/bin/grpc_cli"
}

# vim: set ts=4 sw=4 et :
